apply plugin: 'com.android.application'
apply plugin: 'eclipse'

def keystorePropertiesFile = rootProject.file("../keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    namespace "org.tlhInganHol.android.klingonassistant"
    compileSdkVersion 34
    defaultConfig {
        applicationId "org.tlhInganHol.android.klingonassistant"
        // Target only Tiramisu (33) and above on the Google Play Store, since changes to the permission system are not backwards-compatible.
        // On the Amazon Appstore, target Marshmallow (23) and above.
        minSdkVersion 33
        targetSdkVersion 34
        versionCode 580
        versionName "2024.07.29a"
    }
    buildFeatures {
        buildConfig = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }
}

// First, open any source Java file (with the following line commentd out) to
// run jdtls and generate the project files, including app/.classpath. Next,
// add a kind "src" pointing to "src/main/java" and a kind "lib" pointing to
// "$ANDROID_HOME/platforms/android-xx/android.jar" to app/.classpath. Finally,
// uncomment the following line to prevent buildship from clobbering the
// app/.classpath file when opening a source Java file.
// TODO: There has to be a better way to do this!
// eclipse.classpath.break_buildship = true

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'
    implementation 'com.google.android.material:material:1.12.0'

    // Needed to request permissions on API 33+ (post notifications permissions is needed for KWOTD).
    implementation 'androidx.activity:activity:1.9.1'
    implementation 'androidx.fragment:fragment:1.8.2'
}

task updateDatabase(type: Exec) {
    workingDir '..'
    commandLine './write_db.sh'
}

task checkAudioFiles(type: Exec) {
    workingDir '../../scripts'
    commandLine './check_audio_files.pl'
}

checkAudioFiles.onlyIf { file("../../scripts/check_audio_files.pl").exists() }

preBuild.dependsOn 'updateDatabase'
// preBuild.dependsOn 'checkAudioFiles'

// allprojects {
//     tasks.withType(JavaCompile) {
//         options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
//     }
// }
